#!/usr/bin/perl

# https://github.com/vivien/i3blocks-contrib/blob/master/cpu_usage/cpu_usage

use strict;
use warnings;
use utf8;
use Getopt::Long;

my $t_warn = $ENV{T_WARN} // 50;
my $t_crit = $ENV{T_CRIT} // 80;
my $cpu_usage = -1;
my $decimals = $ENV{DECIMALS} // 2;
my $label = $ENV{LABEL} // "";
my $color_normal = $ENV{COLOR_NORMAL} // "#EBDBB2";
my $color_warn = $ENV{COLOR_WARN} // "#FFFC00";
my $color_crit = $ENV{COLOR_CRIT} // "#FF0000";

sub help {
    print "Usage: cpu_usage [-w <warning>] [-c <critical>] [-d <decimals>]\n";
    print "-w <percent>: warning threshold to become yellow\n";
    print "-c <percent>: critical threshold to become red\n";
    print "-d <decimals>:  Use <decimals> decimals for percentage (default is $decimals) \n"; 
    exit 0;
}

GetOptions("help|h" => \&help,
           "w=i"    => \$t_warn,
           "c=i"    => \$t_crit,
           "d=i"    => \$decimals,
);

$ENV{LC_ALL}="en_US";
open (MPSTAT, 'mpstat 1 1 |') or die;
while (<MPSTAT>) {
    if (/^.*\s+(\d+\.\d+)[\s\x00]?$/) {
        $cpu_usage = 100 - $1;
        last;
    }
}
close(MPSTAT);

$cpu_usage eq -1 and die 'Can\'t find CPU information';

print "${label}";
printf "%.${decimals}f%%\n", $cpu_usage;
print "${label}";
printf "%.${decimals}f%%\n", $cpu_usage;

if ($cpu_usage >= $t_crit) {
    print "${color_crit}\n";
} elsif ($cpu_usage >= $t_warn) {
    print "${color_warn}\n";
} else {
    print "${color_normal}\n";
}

exit 0;